// This file is part of the materials accompanying the book 
// "The Elements of Computing Systems" by Nisan and Schocken, 
// MIT Press. Book site: www.idc.ac.il/tecs
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t]==1) out[t+1] = 0
 * else if (load[t]==1)  out[t+1] = in[t]
 * else if (inc[t]==1)   out[t+1] = out[t] + 1  (integer addition)
 * else                  out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
	Inc16(in=preout,out=incout);
	Mux16(a=preout,b=incout,sel=inc,out=sameOrInc);
	Mux16(a=sameOrInc,b=in,sel=load,out=sameOrIncOrLoad);
	Mux16(a=sameOrIncOrLoad,b[0..15]=false,sel=reset,out=sameOrIncOrLoadOrReset);
	Register(in=sameOrIncOrLoadOrReset,load=true,out=preout);
	
	And16(a[0..15]=true,b=preout,out=out);
}

